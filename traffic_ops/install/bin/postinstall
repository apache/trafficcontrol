#!/usr/bin/perl

#
# Copyright 2015 Comcast Cable Communications Management, LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

use strict;
use warnings;

use lib qw(/opt/traffic_ops/install/lib /opt/traffic_ops/install/lib/perl5 /opt/traffic_ops/app/local/lib/perl5 /opt/traffic_ops/app/lib);
$ENV{PATH}     = "/opt/traffic_ops/install/bin:$ENV{PATH}";
$ENV{PERL5LIB} = "/opt/traffic_ops/install/lib:/opt/traffic_ops/install/lib/perl5:/opt/traffic_ops/app/local/lib/perl5:/opt/traffic_ops/app/lib";

use DBI;
use JSON;
use InstallUtils qw{ :all };
use Digest::SHA1 qw(sha1_hex);
use Data::Dumper;
use File::Temp;
use WWW::Curl::Easy;
use LWP::UserAgent;
use File::Copy;
use Getopt::Long;

my $database_conf     = "/opt/traffic_ops/app/conf/production/database.conf";
my $ldap_conf         = "/opt/traffic_ops/app/conf/ldap.conf";
my $cdn_conf          = "/opt/traffic_ops/app/conf/cdn.conf";
my $migrations_dbconf = "/opt/traffic_ops/app/db/dbconf.yml";
my $post_install_cfg  = "/opt/traffic_ops/install/data/json/post_install.json";
my $users_file        = "/opt/traffic_ops/install/data/json/users.json";
my $profile_dir       = "/opt/traffic_ops/install/data/profiles/";
my %dbdriver          = ( mysql => "mymysql", );

my $reconfigure          = "/opt/traffic_ops/.reconfigure";
my $reconfigure_defaults = "/opt/traffic_ops/.reconfigure_defaults";
my $tmAdminUser          = "";
my $tmAdminPw            = "";
my $parameters;

my $help     = 0;
my $skip_ssl = 0;
GetOptions(
	"help"     => \$help,
	"skip-ssl" => \$skip_ssl
) or die("Error in command line arguments");

my $installMsg = << 'EOF';

This script will initialize the Traffic Ops database.
Please enter the following information in order to completely 
configure the Traffic Ops mysql database.

EOF

sub printUsage {
	print "Traffic Ops Postinstall\n\n";
	print " --help      Show this help text\n";
	print " --skip-ssl  Skip SSL certificate generation\n";
	exit(0);
}

sub readJson {
	my $file = shift;
	open( my $fh, '<', $file ) or return;
	local $/;    # slurp mode
	my $text = <$fh>;
	undef $fh;
	return JSON->new->utf8->decode($text);
}

sub writeJson {
	my $file = shift;
	open( my $fh, '>', $file ) or die("open(): $!");
	foreach my $data (@_) {
		my $json_text = JSON->new->utf8->encode($data);
		print $fh $json_text, "\n";
	}
	close $fh;
}

sub writeYamlToFH {
	my $fh     = shift;
	my $data   = shift;
	my $level  = shift || 0;
	my $prefix = shift || '';

	my $type   = ref($data);
	my $indent = ' ' x $level;
	if ( $type eq '' ) {

		# scalar
		print $fh "$indent$prefix$data\n";
	}
	elsif ( $type eq 'HASH' ) {
		foreach my $key ( keys %$data ) {
			my $value = $data->{$key};
			if ( ref($value) eq '' ) {
				print $fh "$indent$key: $value\n";
			}
			else {
				print $fh "$indent$key:\n";
				writeYamlToFH( $fh, $data->{$key}, $level + 1 );
			}
		}
	}
	elsif ( $type eq 'ARRAY' ) {
		foreach my $d (@$data) {
			writeYamlToFH( $fh, $d, $level + 1, '- ' );
		}
	}
}

sub writeYaml {
	my $file = shift;
	my $data = shift;
	open my $fh, '>', $file or die "open(): $!";
	writeYamlToFH( $fh, $data );
}

# Init.
sub init () {
	my $c      = readJson($database_conf);
	my %dbconf = %$c;
	my $dbAdminUser;
	my $dbAdminPw;

	# loop exits on successful db connect
	while (1) {
		execCommand( "/usr/bin/tput", "clear" );

		if ($DBI::errstr) {
			print "Error connecting to database using the supplied information: $DBI::errstr\n";
		}

		print "\n$installMsg\n";

		$dbconf{type}     = promptUser( "Database type",                       $dbconf{type}     || "mysql" );
		$dbconf{dbname}   = promptUser( "Database name",                       $dbconf{dbname}   || "traffic_ops_db" );
		$dbconf{hostname} = promptUser( "Database server hostname IP or FQDN", $dbconf{hostname} || "localhost" );
		$dbconf{port}     = promptUser( "Database port number",                $dbconf{port}     || "3306" );
		$dbconf{user}     = promptUser( "Traffic Ops database user",           $dbconf{user}     || "traffic_ops" );
		$dbconf{password} = promptPasswordVerify("Password for $dbconf{user}");
		$dbconf{description} = "$dbconf{type} database on $dbconf{hostname}:$dbconf{port}";
		print "\n";
		$dbAdminUser = promptUser( "Database server root (admin) user name", "root" );
		$dbAdminPw = promptPassword("Database server $dbAdminUser password");

		print "Database Type: $dbconf{type}\n";
		print "Database Name: $dbconf{dbname}\n";
		print "Hostname: $dbconf{hostname}\n";
		print "Port: $dbconf{port}\n";
		print "Database User: $dbconf{user}\n";
		my $ans = promptUser( "Is the above information correct (y/n)", "n" );

		if ( $ans eq "y" ) {
			my $dsn = sprintf( "DBI:mysql:%s:%s:%s", "mysql", $dbconf{hostname}, $dbconf{port} );
			my $dbh = DBI->connect( $dsn, $dbAdminUser, $dbAdminPw );
			if ($dbh) {

				# Success!
				$dbh->disconnect();
				last;
			}
		}
	}

	writeJson( $database_conf, \%dbconf );
	print "\nThe database properties have been saved to $database_conf\n";

	# migrations dbconf is in YAML
	my $driver = $dbdriver{ $dbconf{type} };
	my %migrations = ( production => { driver => $driver, open => "tcp:$dbconf{hostname}:$dbconf{port}*$dbconf{dbname}/$dbconf{user}/$dbconf{password}" } );
	writeYaml( $migrations_dbconf, \%migrations );

	my $msg = << 'EOF';

  The database configuration has been saved.  Now we need to set some custom
  fields that are necessary for the CDN to function correctly.

EOF

	print $msg, "\n";
	while (1) {

		my $tmurl = promptUser( "Traffic Ops url", $parameters->{"tm.url"} || "https://localhost" );
		$parameters->{"tm.url"}     = $tmurl;
		$parameters->{"tm.infourl"} = "$tmurl/info";

		$parameters->{cdnname}    = promptUser( "Human-readable CDN Name.  (No whitespace, please)",  $parameters->{cdnname}    || "kabletown_cdn" );
		$parameters->{domainname} = promptUser( "DNS sub-domain for which your CDN is authoritative", $parameters->{domainname} || "cdn1.kabletown.net" );

		my $geolocationUrl = "$tmurl/routing/GeoIP2-City.mmdb.gz";
		$parameters->{"geolocation.polling.url"} = $geolocationUrl;

		my $coverageZoneUrl = "$tmurl/routing/coverage-zone.json";
		$parameters->{"coveragezone.polling.url"} = $coverageZoneUrl;

		my $centosTarballFqn = '';
		my $skip;
		while (1) {
			$centosTarballFqn =
				promptUser( "Fully qualified name of your CentOS ISO kickstart tarball, or 'na' to skip and add files later", "/var/cache/centos72.tgz" );
			if ( $centosTarballFqn eq 'na' ) {
				$skip = 1;
				last;
			}
			if ( -f $centosTarballFqn ) {
				last;
			}
			print "\nNo file named $centosTarballFqn found.\n\n";
		}

		my $kickstartFilesFqn = promptUser( "Fully qualified location to store your ISO kickstart files", "/var/www/files" );
		my $parametersJson = "/opt/traffic_ops/install/data/json/parameter.json";

		## Replace parameter with $kickstartFilesFqn
		open( my $json_fh, "<:encoding(UTF-8)", $parametersJson )
			or die("Can't open \$filename\": $!\n");

		my $json = JSON->new;
		my @json_obj;
		while ( my $json_text = <$json_fh> ) {
			my $data = $json->decode($json_text);

			if ( $data->{"name"} eq "kickstart.files.location" ) {
				$data->{"value"} = $kickstartFilesFqn;
			}
			push @json_obj, $data;
		}
		writeJson( $parametersJson, @json_obj );

		execCommand( "/bin/cp", "/opt/traffic_ops/install/data/perl/osversions.cfg", $kickstartFilesFqn );

		if ( !$skip ) {
			print "\nUncompressing CentOS ISO kickstart tarball.\n";
			print "\nFirst creating $kickstartFilesFqn.\n";
			execCommand( "/bin/mkdir", "-p", $kickstartFilesFqn );
			print "\nUncompressing $centosTarballFqn.\n";
			execCommand( "/bin/tar", "-xzf", $centosTarballFqn, "-C", $kickstartFilesFqn );
		}

		print "\nTraffic Ops URL: $parameters->{'tm.url'}\n";
		print "Traffic Ops Info URL: $parameters->{'tm.infourl'}\n";
		print "Domainname: $parameters->{domainname}\n";
		print "CDN Name: $parameters->{cdnname}\n";
		print "GeoLocation Polling URL: $parameters->{'geolocation.polling.url'}\n";
		print "CoverageZone Polling URL: $parameters->{'coveragezone.polling.url'}\n\n";
		my $ans = promptUser( "Is the above information correct (y/n)", "n" );
		if ( $ans eq 'y' ) {
			last;
		}
	}
	writeJson( $post_install_cfg, $parameters );
	print "Install information has been saved to $post_install_cfg\n\n";

	print "\nAdding an administration user to the Traffic Ops database.\n\n";
	my %user = ();
	$tmAdminUser = promptUser( "Administration username for Traffic Ops", 'admin' );
	$user{username} = $tmAdminUser;
	$tmAdminPw      = promptPasswordVerify("Password for the admin user $tmAdminUser");
	$user{password} = sha1_hex($tmAdminPw);

	writeJson( $users_file, \%user );

	my $ans = promptUser( "Do you wish to create an ldap configuration for access to traffic ops [y/n] ?", "n" );
	if ( $ans eq "y" ) {
		my %ldapconf = readJson($ldap_conf);
		while (1) {
			$ldapconf{host}     = promptUser( "LDAP server hostname", $ldapconf{host}     || "ldap.foobar.com" );
			$ldapconf{admin_dn} = promptUser( "LDAP Admin DN",        $ldapconf{admin_dn} || 'admin@foobar.com' );
			$ldapconf{admin_pass} = promptPasswordVerify("LDAP Admin Password");
			$ldapconf{search_base} = promptUser( "LDAP Search Base", "dc=foobar,dc=com" );
			my $correct = promptUser( "Are the above values correct [y/n]?", "y" );
			if ( $correct eq 'y' ) {
				last;
			}
		}
		writeJson( $ldap_conf, \%ldapconf );
		print "The ldap configuration has been saved.\n\n";
	}

	# Prompt for new secret
	writeSecret($cdn_conf);

	#
	# Call mysql initialization script.
	#
	print "Creating database\n";
	my $result = execCommand( "/opt/traffic_ops/install/bin/create_db", $dbAdminUser, $dbAdminPw );
	if ( $result != 0 ) {
		print "failed to create the database.\n";
		exit 1;
	}

	print "Setting up database\n";
	chdir("/opt/traffic_ops/app");
	$result = execCommand( "/usr/bin/perl", "db/admin.pl", "--env=production", "setup" );

	if ( $result != 0 ) {
		print "Database initialization failed.\n";
		exit 2;
	}
	else {
		print "Database initialization succeeded.\n";
	}

	$result = execCommand( "/opt/traffic_ops/install/bin/dataload", $dbAdminUser, $dbAdminPw );
	if ( $result != 0 ) {
		print "failed to load seed data.\n";
		exit 1;
	}

	print "Downloading MaxMind data.\n";
	chdir("/opt/traffic_ops/app/public/routing");
	$result = execCommand("/usr/bin/wget http://geolite.maxmind.com/download/geoip/database/GeoLite2-City.mmdb.gz");
	if ( $result != 0 ) {
		print "failed to download MaxMind data.\n";

		# exit 1;
	}

	print "Copying coverage zone file to public dir.\n";
	$result = execCommand("/bin/mv /opt/traffic_ops/app/public/coverage-zone.json .");
	if ( $result != 0 ) {
		print "failed to copy coverage zone file.\n";

		# exit 1;
	}
        
	if ( $skip_ssl == 0 ) {
		if ( -x "/usr/bin/openssl" ) {
			print "\nInstalling SSL Certificates.\n\n";
			$result = execCommand("/opt/traffic_ops/install/bin/generateCert");

			if ( $result != 0 ) {
				print "\nSSL Certificate Installation failed.\n";
				exit 3;
			}
			else {
				print "\nSSL Certificates have been installed.\n";
			}
		}
		else {
			print "Unable to install SSL certificates as openssl is not installed.\n";
			print "Install openssl and then run /opt/traffic_ops/install/bin/generateCert to install SSL certificates.\n";
			exit 4;
		}
	}
}    # end of Init

sub writeSecret {
	print "\n\nTraffic Ops requires a secret key to generate authentication cookies.\n\n";

	# read conf file -- see if secrets already there
	my $cdnh = do $cdn_conf;
	unless ( ref($cdnh) eq 'HASH' ) {
		my $err = $@ || $! || ' -- not a HASH';
		if ($err) {
			print "Could not load $cdn_conf $err";
			exit 4;
		}
	}

	# newSecret
	my $secrets = $cdnh->{secrets};
	if ( ( ref $secrets eq 'ARRAY' ) && scalar @$secrets > 0 ) {
		print "One or more secrets found in $cdn_conf.\n";
		my $ans = promptUser( " Do want to add a new one (only 2 will be kept) [y/n] ?", "y" );
		if ( $ans eq "n" ) {

			# nothing further to do...
			return;
		}
	}
	my $new_secret = "";
	while ( length $new_secret == 0 ) {
		print "Adding a new secret.\n";
		my $ans = promptUser( " Do you want one generated for you [y/n] ?", "y" );
		if ( $ans eq "n" ) {
			$new_secret = promptUser( "Secret key:", "" );
		}
		else {

			# create random word 12 chars long
			$new_secret = randomWord(12);
		}
	}

	# keep 2 at most..
	unshift( @$secrets, $new_secret );
	if ( scalar @$secrets > 2 ) {
		$#{$secrets} = 1;
	}

	# dump conf data in compact but readable form
	my $dumper = Data::Dumper->new( [$cdnh] );
	$dumper->Indent(1)->Terse(1)->Quotekeys(0);

	# write whole config to temp file in pwd (keeps in same filesystem)
	my $tmpfile = File::Temp->new( DIR => '.' );
	print $tmpfile $dumper->Dump();
	close $tmpfile;

	# rename current config file to something unique so it's not lost
	my $backup_num = 0;
	my $backup_name;
	do {
		$backup_num++;
		$backup_name = "$cdn_conf.backup$backup_num";
	} while ( -e $backup_name );
	rename( $cdn_conf, $backup_name ) or die("rename(): $!");

	# rename temp file to cdn.conf and set ownership/permissions same as backup
	my @stats = stat($backup_name);
	my ( $uid, $gid, $perm ) = @stats[ 4, 5, 2 ];
	rename( $tmpfile, $cdn_conf ) or die("rename(): $!");

	chown $uid, $gid, $cdn_conf;
	chmod $perm, $cdn_conf;
}

if ( $help == 1 ) {
	printUsage();
}

chdir("/opt/traffic_ops/install/bin");

$parameters = readJson($post_install_cfg);
if ( -f $reconfigure ) {
	my $rc = execCommand( "/opt/traffic_ops/install/bin/build_trafficops_perl_library", "-i" );
	if ( $rc != 0 ) {
		print "ERROR: failed to install perl dependencies, check the console output and rerun postinstall once you've resolved the error.\n";
		exit 5;
	}
	$rc = execCommand( "./download_web_deps", "-i" );
	if ( $rc != 0 ) {
		print "ERROR: failed to install Traffic Ops Web dependencies, check the console output and rerun postinstall once you've resolved the error.\n";
	}
	init();
	unlink($reconfigure);
}
else {
	my $rc = execCommand("/opt/traffic_ops/install/bin/build_trafficops_perl_library");
	if ( $rc != 0 ) {
		print "ERROR: failed to install perl dependencies, check the console output and rerun postinstall once you've resolved the error.\n";
		exit 6;
	}
	$rc = execCommand( "./download_web_deps", "-i" );
	if ( $rc != 0 ) {
		print "ERROR: failed to install Traffic Ops Web dependencies, check the console output and rerun postinstall once you've resolved the error.\n";
	}
}

sub profile_replace {
	my ($profile) = @_;
	my $profile_bak = $profile . ".bak";
	rename( $profile, $profile_bak ) or die("rename(): $!");
	open( my $fh,  '<', $profile_bak ) or die("open(): $!");
	open( my $ofh, '>', $profile )     or die("open(): $!");
	while (<$fh>) {
		s/{{.TmUrl}}/$parameters->{'tm.url'}/g;
		s/{{.TmInfoUrl}}/$parameters->{"tminfo.url"}/g;
		s/{{.TmInstanceName}}/$parameters->{"cdnname"}/g;
		s/{{.GeolocationPollingUrl}}/$parameters->{"geolocation.polling.url"}/g;
		s/{{.Geolocation6PollingUrl}}/$parameters->{"geolocation6.polling.url"}/g;
		s/{{.TmUrl}}/$parameters->{'tm.url'}/g;
		s/{{.TmToolName}}/Traffic Ops/g;
		s/{{.HealthPollingInterval}}/$parameters->{"health.polling.interval"}/g;
		s/{{.CoveragezonePollingUrl}}/$parameters->{"coveragezone.polling.url"}/g;
		s/{{.DomainName}}/$parameters->{"domainname"}/g;
		s/{{.TldSoaAdmin}}/$parameters->{"tld.soa.admin"}/g;
		s/{{.DrivePrefix}}/$parameters->{"Drive_Prefix"}/g;
		s/{{.HealthThresholdLoadavg}}/$parameters->{"health.threshold.loadavg"}/g;
		s/{{.HealthThresholdAvailableBandwidthInKbps}}/$parameters->{"health.threshold.availableBandwidthInKbps"}/g;
		s/{{.RAMDrivePrefix}}/$parameters->{"RAM_Drive_Prefix"}/g;
		s/{{.RAMDriveLetters}}/$parameters->{"RAM_Drive_Letters"}/g;
		s/{{.HealthConnectionTimeout}}/$parameters->{"health.connection.timeout"}/g;
		s#{{.CronOrtSyncds}}#*/15 * * * * root /opt/ort/traffic_ops_ort.pl syncds warn $parameters->{'tm.url'} $tmAdminUser:$tmAdminPw > /tmp/ort/syncds.log 2>&1#g;
		print $ofh $_;
	}
	close $fh;
	close $ofh;
	unlink $profile_bak;
}

sub replace_profile_templates() {
	while ( !defined $parameters->{'tm.url'} || $parameters->{'tm.url'} eq "" ) {
		$parameters->{'tm.url'} = InstallUtils::promptUser( "Traffic Ops url", "https://localhost" );
	}
	while ( !defined $parameters->{"tminfo.url"} || $parameters->{"tminfo.url"} eq "" ) {
		$parameters->{"tminfo.url"} = "$parameters->{'tm.url'}/info";
	}
	while ( !defined $parameters->{"cdnname"} || $parameters->{"cdnname"} eq "" ) {
		$parameters->{"cdnname"} = InstallUtils::promptUser( "Human-readable CDN Name.  (No whitespace, please)", "kabletown_cdn" );
	}
	while ( !defined $parameters->{"geolocation.polling.url"} || $parameters->{"geolocation.polling.url"} eq "" ) {
		$parameters->{"geolocation.polling.url"} = "$parameters->{'tm.url'}/routing/GeoIP2-City.mmdb.gz";
	}
	while ( !defined $parameters->{"geolocation6.polling.url"} || $parameters->{"geolocation6.polling.url"} eq "" ) {
		$parameters->{"geolocation6.polling.url"} = "$parameters->{'tm.url'}/routing/GeoIP2-Cityv6.mmdb.gz";
	}
	while ( !defined $parameters->{"health.polling.interval"} || $parameters->{"health.polling.interval"} eq "" ) {
		$parameters->{"health.polling.interval"} = InstallUtils::promptUser( "Health Polling Interval (milliseconds)", "8000" );
	}
	while ( !defined $parameters->{"coveragezone.polling.url"} || $parameters->{"coveragezone.polling.url"} eq "" ) {
		$parameters->{"coveragezone.polling.url"} = "$parameters->{'tm.url'}/routing/coverage-zone.json";
	}
	while ( !defined $parameters->{"domainname"} || $parameters->{"domainname"} eq "" ) {
		$parameters->{"domainname"} = InstallUtils::promptUser( "DNS sub-domain for which your CDN is authoritative", "cdn1.kabletown.net" );
	}
	while ( !defined $parameters->{"tld.soa.admin"} || $parameters->{"tld.soa.admin"} eq "" ) {
		$parameters->{"tld.soa.admin"} = InstallUtils::promptUser( "TLD SOA admin", "traffic_ops" );
	}
	while ( !defined $parameters->{"Drive_Prefix"} || $parameters->{"Drive_Prefix"} eq "" ) {
		$parameters->{"Drive_Prefix"} = InstallUtils::promptUser( "TrafficServer Drive Prefix", "/dev/sd" );
	}
	while ( !defined $parameters->{"RAM_Drive_Prefix"} || $parameters->{"RAM_Drive_Prefix"} eq "" ) {
		$parameters->{"RAM_Drive_Prefix"} = InstallUtils::promptUser( "TrafficServer RAM Drive Prefix", "/dev/ram" );
	}
	while ( !defined $parameters->{"RAM_Drive_Letters"} || $parameters->{"RAM_Drive_Letters"} eq "" ) {
		$parameters->{"RAM_Drive_Letters"} = InstallUtils::promptUser( "TrafficServer RAM Drive Letters (comma separated)", "0,1,2,3,4,5,6,7" );
	}
	while ( !defined $parameters->{"health.threshold.loadavg"} || $parameters->{"health.threshold.loadavg"} eq "" ) {
		$parameters->{"health.threshold.loadavg"} = InstallUtils::promptUser( "Health Threshold Load Average", "25" );
	}
	while (!defined $parameters->{"health.threshold.availableBandwidthInKbps"}
		|| $parameters->{"health.threshold.availableBandwidthInKbps"} eq ""
		|| $parameters->{"health.threshold.availableBandwidthInKbps"} eq ">" )
	{
		$parameters->{"health.threshold.availableBandwidthInKbps"} =
			">" . InstallUtils::promptUser( "Health Threshold Available Bandwidth in Kbps", "1750000" );
	}
	while ( !defined $parameters->{"health.connection.timeout"} || $parameters->{"health.connection.timeout"} eq "" ) {
		$parameters->{"health.connection.timeout"} = InstallUtils::promptUser( "Traffic Server Health Connection Timeout (milliseconds)", "2000" );
	}

	profile_replace( $profile_dir . "profile.global.traffic_ops" );
	profile_replace( $profile_dir . "profile.traffic_monitor.traffic_ops" );
	profile_replace( $profile_dir . "profile.traffic_router.traffic_ops" );
	profile_replace( $profile_dir . "profile.trafficserver_edge.traffic_ops" );
	profile_replace( $profile_dir . "profile.trafficserver_mid.traffic_ops" );
	writeJson( $post_install_cfg, $parameters );
}

# Takes the Traffic Ops URI, user, and password.
# Returns the cookie, or the empty string on error
sub get_traffic_ops_cookie {
	my ( $uri, $user, $pass ) = @_;

	my $loginUri = "/api/1.2/user/login";

	my $curl          = WWW::Curl::Easy->new;
	my $response_body = "";
	open( my $fileb, ">", \$response_body );
	my $loginData = JSON::encode_json( { u => $user, p => $pass } );
	$curl->setopt( WWW::Curl::Easy::CURLOPT_URL,            $uri . $loginUri );
	$curl->setopt( WWW::Curl::Easy::CURLOPT_SSL_VERIFYPEER, 0 );
	$curl->setopt( WWW::Curl::Easy::CURLOPT_HEADER,         1 );                  # include header in response
	$curl->setopt( WWW::Curl::Easy::CURLOPT_NOBODY,         1 );                  # disclude body in response
	$curl->setopt( WWW::Curl::Easy::CURLOPT_POST,           1 );
	$curl->setopt( WWW::Curl::Easy::CURLOPT_POSTFIELDS,     $loginData );
	$curl->setopt( WWW::Curl::Easy::CURLOPT_WRITEDATA,      $fileb );             # put response in this var
	$curl->perform();

	my $cookie = $response_body;
	if ( $cookie =~ /mojolicious=(.*); expires/ ) {
		$cookie = $1;
	}
	else {
		$cookie = "";
	}
	return $cookie;
}

# Takes the filename of a Traffic Ops (TO) profile to import, the TO URI, and the TO login cookie
sub profile_import_single {
	my ( $profileFilename, $uri, $trafficOpsCookie ) = @_;
	print "Importing Profiles with: "
		. "curl -v -k -X POST -H \"Cookie: mojolicious=$trafficOpsCookie\" -F \"filename=$profileFilename\" -F \"profile_to_import=\@$profileFilename\" $uri/profile/doI\
mport";
	my $rc = execCommand(
		"curl -v -k -X POST -H \"Cookie: mojolicious=$trafficOpsCookie\" -F \"filename=$profileFilename\" -F \"profile_to_import=\@$profileFilename\" $uri/profile/doImport"
	);
	if ( $rc != 0 ) {
		print "ERROR: failed to import Traffic Ops profile, check the console output and rerun postinstall once you've resolved the error.\n";
	}
}

sub import_profiles() {
	while ( length $tmAdminUser == 0 ) {
		$tmAdminUser = InstallUtils::promptUser("Administration username for Traffic Ops");
	}
	while ( $tmAdminPw eq "" ) {
		$tmAdminPw = InstallUtils::promptUser( "Password for the admin user $tmAdminUser", "", 1 );
	}
	while ( !defined $parameters->{'tm.url'} || length $parameters->{'tm.url'} == 0 ) {
		$parameters->{'tm.url'} = InstallUtils::promptUser( "Traffic Ops url", "https://localhost" );
	}

	print "Importing profiles...\n";

	# \todo take as params
	my $toUri  = $parameters->{'tm.url'};
	my $toUser = $tmAdminUser;
	my $toPass = $tmAdminPw;

	my $toCookie = get_traffic_ops_cookie( $toUri, $toUser, $toPass );

	print "Got cookie: " . $toCookie;

	# \todo use an array?
	print "Importing Global profile...\n";
	profile_import_single( $profile_dir . "profile.global.traffic_ops", $toUri, $toCookie );
	print "Importing Traffic Monitor profile...\n";
	profile_import_single( $profile_dir . "profile.traffic_monitor.traffic_ops", $toUri, $toCookie );
	print "Importing Traffic Router profile...\n";
	profile_import_single( $profile_dir . "profile.traffic_router.traffic_ops", $toUri, $toCookie );
	print "Importing TrafficServer Edge profile...\n";
	profile_import_single( $profile_dir . "profile.trafficserver_edge.traffic_ops", $toUri, $toCookie );
	print "Importing TrafficServer Mid profile...\n";
	profile_import_single( $profile_dir . "profile.trafficserver_mid.traffic_ops", $toUri, $toCookie );
	print "Finished Importing Profiles.\n";
}

print "\nStarting Traffic Ops.\n\n";
execCommand("/sbin/service traffic_ops start");

print "\nWaiting for Traffic Ops to start.\n\n";
sleep(5);

sub profiles_exist {
	if ( -f $reconfigure_defaults ) {
		print "Default profiles were previously created. Remove " . $reconfigure_defaults . " to create again.\n";
		return 1;
	}

	while ( length $tmAdminUser == 0 ) {
		$tmAdminUser = InstallUtils::promptUser("Administration username for Traffic Ops");
	}
	while ( $tmAdminPw eq "" ) {
		$tmAdminPw = InstallUtils::promptUser( "Password for the admin user $tmAdminUser", "", 1 );
	}
	while ( !defined $parameters->{'tm.url'}
		|| length $parameters->{'tm.url'} == 0 )
	{
		$parameters->{'tm.url'} = InstallUtils::promptUser( "Traffic Ops url", "https://localhost" );
	}

	my $uri = $parameters->{'tm.url'};
	my $toCookie = get_traffic_ops_cookie( $parameters->{'tm.url'}, $tmAdminUser, $tmAdminPw );

	my $profileEndpoint = "/api/1.2/profiles.json";

	my $ua = LWP::UserAgent->new;
	$ua->ssl_opts( verify_hostname => 0, SSL_verify_mode => 0x00 );
	my $req = HTTP::Request->new( GET => $uri . $profileEndpoint );
	$req->header( 'Cookie' => "mojolicious=" . $toCookie );
	my $resp = $ua->request($req);

	if ( !$resp->is_success ) {
		print "Error checking if profiles exist: " . $resp->status_line . "\n";
		return 1;    # return true, so we don't attempt to create profiles
	}
	my $message = $resp->decoded_content;

	my $profiles = JSON->new->utf8->decode($message);
	if (   ( !defined $profiles->{"response"} )
		|| ( ref $profiles->{"response"} ne 'ARRAY' ) )
	{
		print "Error checking if profiles exist: invalid JSON: $message\n";
		return 1;    # return true, so we don't attempt to create profiles
	}

	my $num_profiles = scalar( @{ $profiles->{"response"} } );
	print "Existing Profile Count: $num_profiles\n";

	my %initial_profiles = (
		"INFLUXDB"       => 1,
		"RIAK_ALL"       => 1,
		"TRAFFIC_STATS"  => 1,
		"TRAFFIC_PORTAL" => 1
	);

	my $profiles_response = $profiles->{"response"};
	foreach my $profile (@$profiles_response) {
		if ( !exists $initial_profiles{ $profile->{"name"} } ) {
			print "Found existing profile (" . $profile->{"name"} . ")\n";
			open( my $reconfigure_defaults_file, '>', $reconfigure_defaults ) or die("Failed to open() $reconfigure_defaults: $!");
			close($reconfigure_defaults_file);
			return 1;
		}
	}
	return 0;
}

if ( !profiles_exist() ) {
	print "Creating default profiles...\n";
	replace_profile_templates();
	import_profiles();
	profiles_exist();    # call again to create $reconfigure_defaults file if import was successful
}
else {
	print "Not creating default profiles.\n";
}

#print "\nRunning smoke tests.\n\n";
#$rc = execCommand ("/opt/traffic_ops/install/bin/systemtest", "localhost", $user{username}, $tmAdminPw, "0");
{
	my $ans = promptUser( "\nInstall Cron entry to clean install .iso files older than 7 days? [y/n]", "n" );
	if ( $ans eq "y" || $ans eq "Y" ) {
		execCommand(
			"/bin/echo \"00 04 * * * root /bin/find /opt/traffic_ops/app/public/iso/*.iso -mtime +7 -exec /bin/rm {} \; > /dev/null 2>&1 \" > /etc/cron.d/trafops_clean_isos"
		);
	}
}

{
	my $ans = promptUser( "\nShutdown Traffic Ops [y/n]", "n" );
	if ( $ans eq "y" ) {
		print "\nShutting down Traffic Ops.\n\n";
		execCommand( "/sbin/service", "traffic_ops", "stop" );
	}
}

print "\nTo start Traffic Ops:  service traffic_ops start\n";
print "To stop Traffic Ops:   service traffic_ops stop\n";
print "\n";

exit 0;
